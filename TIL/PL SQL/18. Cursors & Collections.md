
> 커서 : 단일 로우가 아닌 멀티로우를 처리하도록 한다.
> 컬렉션 : 배열 계열


# 커서

> **커서(Cursor)**는 Oracle이 쿼리 결과를 저장하고, 한 줄씩 접근할 수 있게 해주는 **메모리 구조**

## Implicit Cursor (묵시적 커서)

> 묵시적 커서는 PL/SQL에 의해 생성되고 관리되는 세션 커서이다.
> 
> PL/SQL은 SELECT, DML문을 실행할 때마다 묵시적 커서를 연다.
> 묵시적 커서를 제어할수 없지만 해당 속성에 정보를 얻을 수 있다.


| 명령               | 역할                                            |
| ---------------- | --------------------------------------------- |
| SQL%ISOPEN       | 현재 커서가 열려 있는지 여부를 나타냄 (TRUE/FALSE)            |
| **SQL%FOUND**    | 마지막 SQL 문이 결과를 반환했는지 여부를 나타냄 (TRUE/FALSE)     |
| SQL%NOTFOUND     | 마지막 SQL 문이 결과를 반환하지 않았는지 여부를 나타냄 (TRUE/FALSE) |
| **SQL%ROWCOUNT** | 마지막 SQL 문에 의해 영향을 받은 행의 수를 반환함                |
|                  |                                               |

사용 예시
``` SQL

DECLARE
    CNT NUMBER;
BEGIN

    UPDATE REAL_ORD
    SET REG_DAY = SYSDATE
    WHERE CST_ID = 'C00S3';
    
    IF SQL%FOUND THEN
        DBMS_OUTPUT.PUT_LINE('UPDATE DATA CNT : '||SQL%ROWCOUNT);
    ELSE
        DBMS_OUTPUT.PUT_LINE('UPDATE DATA CNT : 0');
    END IF;
END;
/
```



---
## Explicit Cursors (명시적 커서)

> **명시적 커서**란, 개발자가 `SELECT` 결과를 **직접 처리**하기 위해 **명시적으로 선언하고 사용하는 커서**를 말한다.

### 구조 정리 (4단계)
``` SQL
-- 1. 커서 선언 
CURSOR cursor_name IS     SELECT ...;  

-- 2. 커서 열기
OPEN cursor_name;  

-- 3. 데이터 가져오기
FETCH cursor_name INTO 변수;  

-- 4. 커서 닫기 
CLOSE cursor_name;
```

### 선언 위치

`DECLARE` 블록 내부

사용 예시
``` sql
DECLARE
    p_cst_id varchar2(20);
BEGIN
    p_cst_id := null;
    
    for fc in ( select cst_id, name from cst_info where cst_id = nvl(p_cst_id,cst_id))
    loop
        dbms_output.put_line(fc.cst_id || '-' || fc.name);
    end loop;
    
END;
```

for loop와 함께 사용하면 코드 수가 줄어든다.
실무에서 많이 사용하는 방법이라고 함.
`fc` 에는 괄호 안에 쿼리문 결과가 받아진다.


## strong 타입의 선언 변수 (사용 권장)
> **리턴 타입이 정해지지 않으면 week 타입이다.**

장점
- 타입 안정성(Type Safety)
- **스키마 변경에 강함**
	- `cst_info` 테이블에 컬럼이 추가/변경되면, `%ROWTYPE`이 자동 반영돼서 변경 관리가 쉬움. `Record` 타입 사용도 마찬가지.
- 가독성/유지보수성 향상
- 레코드 단위 처리에 유리 

프로시저에서 특정 테이블의 전체 정보를 레코드 단위로 다룰 때 사용하는 것이 좋다고 함.
쿼리 유연성이 필요하다면 **weak 타입 REF CURSOR**를 쓰는 게 나음

``` sql
DECLARE
    p_cst_id varchar2(20);
    -- 1. 커서 선언?
    type cstcurtyp is ref cursor return cst_info%rowtype;
    cur_cst_info cstcurtyp;
    
    v_cst_id cst_info.cst_id%type;
    v_name cst_info.name%type;  
    r_cst_info cst_info%rowtype;
    
BEGIN
    p_cst_id := null;
    -- 2. 커서 열기
    open cur_cst_info
        for select * from cst_info where cst_id = nvl(p_cst_id, cst_id);
            Loop
                -- 3. 데이터 가져오기.
                fetch cur_cst_info into r_cst_info;
                exit when cur_cst_info%notfound;
                dbms_output.put_line(r_cst_info.cst_id || '-' || r_cst_info.name);
            end loop;
    -- 4. 커서 닫기.
    close cur_cst_info;

END;
```


---

# 커서 리턴받아 사용 예시 
> 커서를 닫는 close는 마지막 사용하는 측에서 닫아줘야 함.

## 구조 요약 

| 커서 종류                  | 리턴 방식                                   | 처리 방식                          |
| ---------------------- | --------------------------------------- | ------------------------------ |
| `PF_SECTION7_CURSOR`   | **Strong Type REF CURSOR (레코드 기반)**     | 커서를 리턴받아 `FETCH INTO`로 한 줄씩 꺼냄 |
| `PSP_SECTION7_CURSOR`  | **Strong Type REF CURSOR (`%ROWTYPE`)** | 커서를 리턴받아 반복 처리                 |
| `PSP_SECTION7_CURSOR2` | **Weak Type REF CURSOR**                | 다양한 컬럼을 유연하게 꺼냄                |


P_COMM 공통으로 사용되는 패키지.
``` sql
create or replace PACKAGE P_COMM AS 

  /*공통으로 사용*/ 
  /* 패키지 내부 전역 레코드 */
    TYPE CstInfoRecTyp IS RECORD (
        v_name   cst_info.name%type,
        v_birth  cst_info.birth%type,
        v_mobile cst_info.mobile%type,
        v_favorite varchar2(100)
   );
   
   /* weak type */
   TYPE rtn_cur IS REF CURSOR ;
   
   /* Strong type cst_info - Section7 리턴값이 있으면 strong타입 */
   TYPE cstcurtyp IS REF CURSOR RETURN cst_info%ROWTYPE;
   
   TYPE rec_cur IS REF CURSOR RETURN CstInfoRecTyp;

END P_COMM;
```



``` sql
CREATE OR REPLACE
PACKAGE BODY P_SECTION7 AS

--    /* function, cursor return, record 사용*/
    function PF_SECTION7_CURSOR(
        P_CST_ID IN CST_INFO.CST_ID%TYPE
    ) return P_COMM.rec_cur
    as
        rec_cur P_COMM.rec_cur; -- 레코드 타입 리턴하는 커서임.
    begin
        -- strong type 
        OPEN  rec_cur 
            for  select name, birth, mobile, '커피' as favorite from cst_info;
            --where cst_id = P_CST_ID;
        return rec_cur;
    end PF_SECTION7_CURSOR;

    -- 프로시저 커서 리턴
    Procedure PSP_SECTION7_CURSOR(
        P_CST_ID IN cst_info.cst_id%TYPE,
        C_CST OUT P_COMM.cstcurtyp
    ) AS
    BEGIN
        -- strong type
        open c_cst
            for select * from cst_info;
    END PSP_SECTION7_CURSOR;


    /* 프로시저, 커서 리턴, one row에 대한 유용한 사용
    주문서 정보 기반의 값들 추출
    */
    PROCEDURE PSP_SECTION7_CURSOR2 (
        p_ord_no in real_ord.ord_no%type,
        rtn_cur out P_COMM.rtn_cur
      )
    AS
      
    BEGIN
        open rtn_cur   
        for select a.ord_no, a.cst_id, c.name, a.mnu_id,
            (select x.mnu_nm from menu x where x.mnu_id=a.mnu_id) as mnu_nm,
            a.price, a.total_price, a.reg_day
            from real_ord a, cst_info c
            where a.cst_id=c.cst_id
            and ord_no=p_ord_no;
            
    END PSP_SECTION7_CURSOR2;
    
    
    /*
    명시적 커서, 변수 사용
    */
    PROCEDURE PSP_SECTION7_CURSOR AS
        p_cst_id cst_info.cst_id%type;          -- 고객 id
        c_cst P_COMM.cstcurtyp;               -- 고객 정보타입
        r_cst cst_info%rowtype;                 -- 고객 정보타입
        
        rec_cur P_COMM.rec_cur;                 -- 레코드 타입 커서
        rec_cst_info P_COMM.CstInfoRecTyp;  -- 고객정보 레코드 타입
        
        --  week type cursor
        rtn_cur P_COMM.rtn_cur;                 -- 반환 커서
        r_real_ord real_ord%rowtype;            -- 주문 로우 타입
        r_menu menu%rowtype;                -- 메뉴 로우타입
        
    BEGIN 
        -- strong, record type cursor return
        
        rec_cur := PF_SECTION7_CURSOR(p_cst_id); -- 커서 반환받음.
            loop
                fetch rec_cur into rec_cst_info;
                exit when rec_cur%NOTFOUND;
                dbms_output.put_line('line 74 : '|| rec_cst_info.v_mobile);
                dbms_output.put_line('line 75 : '|| rec_cst_info.v_favorite);
            end loop;
        close rec_cur;
        
     dbms_output.put_line(chr(10)||chr(13));

    -- strong type return
    PSP_SECTION7_CURSOR(p_cst_id, c_cst);
        loop
            fetch c_cst into r_cst;
            exit when c_cst%notfound;
            dbms_output.put_line('line 86 : '||r_cst.name);
        end loop;
        close c_cst;
       dbms_output.put_line('----------------------------');

    /* 
    weak type 변수 return 
    커서의 특성을 이용하여 하나의 row 를 유연하게 사용가능 
    */ 
    
    r_real_ord.ord_no := '1000035';
    PSP_SECTION7_CURSOR2(r_real_ord.ord_no, rtn_cur);
        fetch rtn_cur
            into r_real_ord.ord_no, r_real_ord.cst_id, r_cst.name, r_real_ord.mnu_id,
            r_menu.mnu_nm, r_real_ord.price, r_real_ord.total_price, r_real_ord.reg_day;
        dbms_output.put_line('Line99 : '||r_real_ord.ord_no);
              dbms_output.put_line('Line100 : '||r_cst.name);
              dbms_output.put_line('Line101 : '||r_menu.mnu_nm);
        close rtn_cur;
    
    END PSP_SECTION7_CURSOR ;
END P_SECTION7;

```




---

# 비교: 암묵적 커서 vs 명시적 커서

| 구분  | 암묵적 커서      | 명시적 커서      |
| --- | ----------- | ----------- |
| 선언  | ❌ 없음        | ✅ 직접 선언     |
| 제어  | Oracle이 자동  | 사용자가 직접 제어  |
| 대상  | 단일 행 처리     | 다중 행 처리     |
| 속성  | SQL%FOUND 등 | 커서명%FOUND 등 |
